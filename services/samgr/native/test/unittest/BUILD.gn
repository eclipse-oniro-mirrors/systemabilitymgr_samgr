# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")

module_output_path = "samgr/samgr"
samgr_dir = "//foundation/distributedschedule/samgr"
samgr_services_dir = "${samgr_dir}/services/samgr/native"

config("sam_test_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "${samgr_services_dir}/test/unittest/include",
    "//foundation/distributedschedule/samgr/utils/native/include",
  ]
}

ohos_unittest("SystemAbilityMgrTest") {
  module_out_path = module_output_path

  sources = [
    "${samgr_dir}/utils/native/source/tools.cpp",
    "${samgr_services_dir}/source/ability_death_recipient.cpp",
    "${samgr_services_dir}/source/system_ability_load_callback_proxy.cpp",
    "${samgr_services_dir}/source/system_ability_manager.cpp",
    "${samgr_services_dir}/source/system_ability_manager_stub.cpp",
    "${samgr_services_dir}/source/system_ability_status_change_proxy.cpp",
    "${samgr_services_dir}/test/unittest/src/itest_transaction_service.cpp",
    "${samgr_services_dir}/test/unittest/src/sa_status_change_mock.cpp",
    "${samgr_services_dir}/test/unittest/src/system_ability_mgr_test.cpp",
  ]

  configs = [
    ":sam_test_config",
    "${samgr_dir}/interfaces/innerkits/lsamgr:config_samgr",
    "${samgr_dir}/services/samgr/native:sam_config",
  ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }

  deps = [
    "${samgr_dir}/interfaces/innerkits/common:samgr_common",
    "${samgr_dir}/interfaces/innerkits/lsamgr:lsamgr",
    "${samgr_dir}/interfaces/innerkits/samgr_proxy:samgr_proxy",
    "//third_party/googletest:gtest_main",
  ]

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "eventhandler:libeventhandler",
    "hiviewdfx_hilog_native:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "ipc:libdbinder",
    "utils_base:utils",
  ]
}

ohos_executable("ondemand") {
  sources = [ "./src/ondemand_helper.cpp" ]
  configs = [
    ":sam_test_config",
    "${samgr_dir}/interfaces/innerkits/lsamgr:config_samgr",
  ]
  deps = [ "${samgr_dir}/interfaces/innerkits/samgr_proxy:samgr_proxy" ]
  if (is_standard_system) {
    external_deps = [ "hiviewdfx_hilog_native:libhilog_base" ]
  } else {
    external_deps = [ "hilog:libhilog" ]
  }

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  external_deps += [
    "ipc:ipc_core",
    "samgr_standard:samgr_proxy",
    "utils_base:utils",
  ]
  part_name = "samgr_standard"
  subsystem_name = "distributedschedule"
}

group("unittest") {
  testonly = true
  deps = [
    ":SystemAbilityMgrTest",
    ":ondemand",
  ]
}
